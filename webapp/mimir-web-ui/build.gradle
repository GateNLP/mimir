buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

version "6.2-SNAPSHOT"
group "uk.ac.gate.mimir"

apply plugin: 'java'
apply plugin: 'gwt-compiler'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
  mavenCentral()
}

gwt {
  gwtVersion='2.6.0'
  modules = ['gate.mimir.web.UI']
}

jar {
  from(tasks.compileGwt.outputs) {
    into 'META-INF/assets/gwt'
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourceJar {
        classifier "sources"
      }

      pom.withXml {
        Node pomNode = asNode()

        pomNode.children().last() + {
          delegate.name "Mímir search UI"
          delegate.description """\
            GWT user interface used by the Mímir search web application.
            """
          delegate.url "https://gate.ac.uk/mimir"

          delegate.developers {
            delegate.developer {
              delegate.id "gate-team"
              delegate.name "GATE Team"
              delegate.email "gate-developers@lists.sourceforge.net"
            }
          }

          delegate.licenses {
            delegate.license {
              delegate.name "GNU Lesser General Public License (LGPL), Version 3"
              delegate.url "http://www.gnu.org/licenses/lgpl.html"
              delegate.distribution "repo"
            }
          }
        }
      }
    }
  }

  repositories {
    maven {
      url = "https://repo.gate.ac.uk/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
      credentials {
        username project.hasProperty('gate.repo.username') ? project.property('gate.repo.username') : ''
        password project.hasProperty('gate.repo.password') ? project.property('gate.repo.password') : ''
      }
    }
  }
}
